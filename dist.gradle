

    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'



    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar, javadocJar
    }

    repositories {
        jcenter()
        mavenLocal()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                artifactId project.ext.artifactId

                pom.withXml {
                    def node = asNode()
                    node.appendNode('name', 'LoganSquareX')
                    node.appendNode('description', 'Screaming fast JSON parsing and serialization library for Android.')
                    node.appendNode('url', 'https://gitee.com/LoganSquareX/LoganSquareX')

                    def organizationNode = node.appendNode('organization')
                    organizationNode.appendNode('name', 'BLoganSquareX')
                    organizationNode.appendNode('url', 'https://gitee.com/LoganSquareX')

                    def scmNode = node.appendNode('scm')
                    scmNode.appendNode('connection', 'scm:git:https://gitee.com/LoganSquareX/LoganSquareX')
                    scmNode.appendNode('developerConnection', 'scm:git:https://gitee.com/LoganSquareX/LoganSquareX')
                    scmNode.appendNode('url', 'https://gitee.com/LoganSquareX/LoganSquareX')

                    def developersNode = node.appendNode('developers')
                    def bunny = developersNode.appendNode('developer')
                    bunny.appendNode('id', 'bunnyblue')
                    bunny.appendNode('name', 'BunnyBlue')
                    def zq = developersNode.appendNode('developer')
                    zq.appendNode('id', 'ZQiang94')
                    zq.appendNode('name', 'ZQiang94')

                    def licensesNode = node.appendNode('licenses')
                    def licenseNode = licensesNode.appendNode('license')
                    licenseNode.appendNode('name', 'Apache 2.0')
                    licenseNode.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')

                }
            }
        }
    }

    bintray {
        Properties properties = new Properties()
        File propFile = "${System.properties['user.home']}${File.separator}.gradle${File.separator}bintray.properties" as File
       if (propFile.exists())
       {
           properties.load(propFile.newDataInputStream())
           user = properties.getProperty("bintray.user")
           key = properties.getProperty("bintray.apikey")
       }
        publications = ['mavenJava']

        dryRun = false
        publish = false

        pkg {
            repo = 'LoganSquareX'
            userOrg = 'logansquarex'
            name = project.ext.artifactId
            desc = 'Screaming fast JSON parsing and serialization library for Android.'
            websiteUrl = 'https://gitee.com/LoganSquareX/LoganSquareX'
            issueTrackerUrl = 'https://gitee.com/LoganSquareX/LoganSquareX/issues'
            vcsUrl = 'https://gitee.com/LoganSquareX/LoganSquareX'
            licenses = ['Apache-2.0']
            labels = ['json', 'android', 'parser', 'serializer', 'jackson']
            publicDownloadNumbers = true
            version {
                name = project.version
                gpg {
                    sign = true
                    passphrase = project.hasProperty('bintray_gpg_passphrase') ? bintray_gpg_passphrase : ''
                }
                mavenCentralSync {
                    sync = false
                    user = project.hasProperty('maven_central_username') ? maven_central_username : ''
                    password = project.hasProperty('maven_central_password') ? maven_central_password : ''
                    close = '1'
                }
            }
        }
    }

